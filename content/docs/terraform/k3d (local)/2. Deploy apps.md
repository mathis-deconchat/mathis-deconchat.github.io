---
title: "2. Deploy Apps"
date: 2023-08-25T01:12:20+02:00
draft: false
icon: "code_blocks"
weight: 1800
---

## Create a new directory for the app
{{< alert context="warning" text="Some Kubernetes stuff will be used, it doesn't matter if you don't understand it now. If you want to know more, go to the kubernetes section" />}}
Create the 2 usual files for basic terraform 
```bash
touch provider.tf main.tyf
```

### provider.tf
```terraform
terraform {
  required_providers {

    kubernetes = {
      source  = "hashicorp/kubernetes"
      version = "2.23.0"
    }
  }
}

provider "kubernetes" {
  config_path    = "~/.kube/config"
  config_context = "k3d-simple-cluster"
}
```
We will use the official provider for kubernetes, and we will use the config file generated by k3d to connect to the cluster. And specify the context we want to use.

### main.tf
First, we will create a configMap, it's a way to store arbitrary data in the cluster. We will use it to create a simple html page that we will display later via a nginx server and an ingress.
```terraform
resource "kubernetes_config_map_v1" "configmap_nginx_cfgmap" {
  metadata {
    name = "nginxcfgmap"
  }
  data = {
    "file-from-cfgmap" = <<-EOT
      <!DOCTYPE html>
      <html lang="en">
        <head>
          <title>From config map</title>
        </head>
        <body>
        
          <h1>Hello world from terraformn</h1>
        
        </body>
      </html>
      EOT
  }

}
```
It's a super simple HTML page, but it will be different than the nginx classic page, so we will be able to see the difference.

Then we will create a deployment, it's a way to deploy a pod (a container) in the cluster. We will use the nginx image and mount the configMap we created earlier.
```terraform
esource "kubernetes_deployment" "nginx" {
  metadata {
    name = "nginx"
    labels = {
      app = "nginx"
    }
  }

  spec {
    selector {
      match_labels = {
        app = "nginx"
      }
    }

    replicas = 1

    template {
      metadata {
        labels = {
          app = "nginx"
        }
      }

      spec {
        container {
          name  = "nginx"
          image = "nginx"



          port {
            container_port = 80
          }

          volume_mount {
            name       = "config-volume"
            mount_path = "/usr/share/nginx/html/index.html"
            sub_path   = "index.html"
          }
        }
        volume {
          name = "config-volume"


          config_map {
            name = "nginxcfgmap"
            items {
              key  = "file-from-cfgmap"
              path = "index.html"
            }

          }
        }
      }
    }
  }
}
```
It's ok if not everything is clear.

Then we will create a service, it's a way to expose a pod to the outside world. We will expose the port 80 of the nginx pod.
```terraform
esource "kubernetes_service" "nginx" {
  metadata {
    labels = {

      name = "nginx"
    }
    name = "nginx"
  }

  spec {
    selector = {
      app = "nginx"

    }
    port {
      protocol    = "TCP"
      name        = "http"
      port        = 80
      target_port = 80
    }

    type = "ClusterIP"

  }
}
``` 

And finally we will create an ingress, it's a way to expose a service to the outside world. We will use the nginx ingress controller, and we will expose the service we created earlier.
```terraform
resource "kubernetes_ingress_v1" "nginx" {
  metadata {
    name = "nginx"

    annotations = {
      "ingress.kubernetes.io/ssl-redirect" = "false"
    }
  }

  spec {
    rule {
      http {
        path {
          path      = "/"
          path_type = "Prefix"

          backend {
            service {
              name = "nginx"

              port {
                number = 80
              }
            }
          }
        }
      }
    }
  }
}
```

### Apply the terraform
```bash
tf init 
tf plan 
tf apply
```
Then wait a few minutes that the nginx deployment is ready, and go to http://localhost:8080/ and you should see the nginx page.

or 

```bash 
‚ùØ curl http://localhost:8080/
<!DOCTYPE html>
<html lang="en">
  <head>
    <title>From config map</title>
  </head>
  <body>
        
    <h1>Hello world from terraformn</h1>
        
  </body>
</html>
```